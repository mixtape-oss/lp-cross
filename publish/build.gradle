import org.gradle.api.tasks.bundling.Jar

plugins {
    id 'java-library'
    id 'maven-publish'
}

archivesBaseName = 'lp-cross'

task copyNatives(type: Copy) {
    from rootProject.projectDir
    into new File(projectDir, "src/main/resources")
    include 'natives/**'
}

processResources.dependsOn copyNatives

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

publishing {
    repositories {
        maven {
            name = "jfrog"
            url = "https://dimensional.jfrog.io/artifactory/maven"
            credentials {
                username = System.getenv("JFROG_USERNAME")
                password = System.getenv("JFROG_PASSWORD")
            }
        }
    }

    publications {
        maven(MavenPublication) {
            from components.java

            groupId 'com.github.natanbc'
            artifactId 'lp-cross'
            version rootProject.version

            artifact sourceJar
        }
    }
}

publish {
    onlyIf {
        System.getenv("JFROG_USERNAME") != null && System.getenv("JFROG_PASSWORD") != null
    }
}
